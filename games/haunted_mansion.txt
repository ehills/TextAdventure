Player andy  {
    name =  "Andy";
    description = "An amateur ghost hunter.";
    carryLimit = 4;
}

ItemDefaults {
    Verb "examine or x or ex or look" {
        print inputItem getDescription;
    }
    
	Verb "pickup or pick-up or get" {
		if (inputItem hasAttribute canPickup) {
			 if (andy hasItem inputItem) {
	            print "You already have the ";
	            print inputItem;
	        } else {
	            if (andy canCarry) {
	                inputItem setLocation andy;
					print "You pickup the ";
					print inputItem;
	            } else {
	                print "You are carrying too much already.";
                }
            }
        } else {
        	print "You cannont pick up the ";
        	print inputItem;
        }
    }

    Verb "drop" {
        if (andy hasItem inputItem) {
            inputItem setLocation currentLocation;
			print "You drop the ";
			print inputItem;
        } else {
            print "I don't have ";
            print inputItem;
        }
    }
}

LocationDefaults {
    Verb "look" {
		 if(andy inLocation tortureRoom) {
            print "You look into the face of death and feel the scythe rip through your body. You flop over in agony and slowly die with the last thing you hear being the spectre cackling at your misfortune...";
			gameOver;
		} else {
			list;
			describe;
			
		}
    }
    Verb "north or n or nth" {
    	if(andy inLocation hasNorth) {
    		andy setLocation toNorth;
    		list;
			describe;
    	} else {
    		print "You cannot go north";
    	}
    }
    Verb "south or s or sth" {
    	if(andy inLocation tortureRoom) {
    		print "As you turn to flee you hear a bone-chilling cackling behind you as the apparation swings his scythe. Before you can react you feel a sickening pain in your neck. You have been decapitated, your head rolls and your body crumples to the ground. You are dead..."
    		gameOver;
    	}
    	if(andy inLocation hasSouth) {
    		andy setLocation toSouth;
    		list;
			describe;
    	} else {
    		print "You cannot go south";
    	}
    }
    Verb "east or e" {
    	if(andy inLocation hasEast) {
    		andy setLocation toEast;
    		list;
			describe;
    	} else {
    		print "You cannot go east";
    	}
    }
    Verb "west or w" {
    	if(andy inLocation hasWest) {
    		andy setLocation toWest;
    		list;
			describe;
    	} else {
    		print "You cannot go west";
    	}
    }
}

initialLocation = garden;

initialDescription = "Your name is Andy an amateur ghost hunter. You have arrived at your first job and find yourself in an overgrown garden, to the north is the old, abandoned mansion locals say to be haunted by a sadistic cultist who once inhabited it many years ago. This is the perfect place to hone your ghost-busting skills.";

// Location to store game items
Location deadSpace {
	name = "Dead space";
	description = "Dead space";
}

Location garden {
    name = "The overgrown garden";
    description = "You are in the overgrown garden in front of the mansion.";

    north = entranceHall;
}

Location entranceHall {
    name = "The entrance hall";
    description = "You find yourself in a dimly lit expansive entrance hall illuminated only by the faint moonlight filtering through the dusty windows. You can only see darkness to the north and doorways to the east and west.";

    west = masterBedroom;
    east = diningRoom;
    south = garden;
}

Location diningRoom {
    name = "The dining room";
    description = "You find yourself in an old dusty dining room, complete with ornate chandeliers. To the west is the Entrance hall where you just entered from.";

    west = entranceHall;
}

Location masterBedroom {
    name = "The master bedroom";
    description = "You are surrounded in darkness. You cannot see anything but can hear strange noises close by. A cold chill rushes over you. To the east is the Entrance hall where you just entered from.";
    east = entranceHall;
}

Location secretHall {
    name = "The secret hall";
    description = "A long narrow hallway expands out in front of you, you see an opening at the end of the hall which you can feel a dark forbidding presence coming from it.";

    south = entranceHall;
}

Location tortureRoom {
    name = "The torture room";
    description = "You enter a cold stone room and instantly feel terrified. You see pentagrams carved into the stone floors which look to be covered in fresh blood. A hooded apparition materializes in front of you, wielding a menacing scythe. You can see your death is it's eyes.";

    south = secretHall;
}

Item lamp {
    name = "lamp";
    description = "A rusty old lamp but still looks like it might work.";
    location = diningRoom;
    hasAttributes = "canPickup";

    Verb "light or use or turn-on or turnon" {
		if(lamp inLocation andy) {
			entranceHall setDescription "With light eminating from the lamp you can now make out a doorway to the north as long with the doorways to the east and west.";
			entranceHall setNorth secretHall;
			masterBedroom setDescription "With the aid of the lamp you find yourself in an old creepy bedroom.";
			mirror setLocation masterBedroom;
			bed setLocation masterBedroom;
			print "You fiddle around with the old lamp and manage to turn it on.";
		} else {
			print "You don't have that item.";
		}
    }
	
	Verb "off or turnoff or turn-off" {
		if(lamp inLocation andy) {
			entranceHall setDescription "You find yourself in a dimly lit expansive  entrance hall, only illuminated by the faint moonlight filtering through the dusty windows. You can only make out darkness to the north.";
			entranceHall removeNorth;
			masterBedroom setDescription "You are surrounded in darkness. You cannot see anything but can hear strange noises close by. A cold chill rushes over you.";
			print "You switch off the old lamp.";
		} else {
			print "You don't have that item.";
		}
    }
}

Item mirror {
    name = "mirror";
    description = "You see your handsome, confident reflection in the mirror. This mirror could be useful...";
    location = deadSpace;
    hasAttributes = "canPickup";

    Verb = "use or lookat or look-at" {
        if(andy inLocation tortureRoom) {
            print "You pull out out the mirror, the ghost upon seeing it's horrific reflection flees in terror. Congratulations you have busted your first ghost!";
            gameOver;
        } else {
            print "You admire yourself in the mirror.";
        }
    }
}

Item door {
	name = "door"
	description = "A solid wooden door is located in the north wall. You notice a small keyhole";
	location = secretHall;
	hasAttributes = "isLocked !canPickup";
	
	Verb = "open or unlock" {
		if (door hasAttribute isLocked) {
		print "You cannot open the door it is locked";
		} else {
			secretHall setNorth tortureRoom;
			print "The door swings open";
		}
	}
}

Item key {
	name = "key";
	description = "a small key";
	location = deadSpace;
	hasAttributes = "canPickup";
	
	Verb = "use" {
		if(andy inLocation secretHall) {
			print "You struggle to turn the key in the old door but manage to unlock it";
			secretHall setNorth tortureRoom;
			door setAttribute !isLocked;
		}
	}
}

Item bed {
	name = "bed";
	description = "A massive bed... you can see something glinting in the lamp light under it";
	location = deadSpace;
	hasAttributes = "!canPickup";
	
	Verb = "under or look or examine or x or ex" {
		if (key inLocation andy) {
			print " A massive bed";
		} else {
			print "You see a small key glinting under the bed";
			key setLocation masterBedroom;	
		}
	}
}

